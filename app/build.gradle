apply plugin: 'com.android.application'

repositories {
    google()
}

android {
    compileSdkVersion 30
    buildToolsVersion '30.0.2'

    defaultConfig {
        applicationId "tech.salroid.filmy"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 17
        versionName "2.1.3"
        multiDexEnabled true
        resConfigs "en", "de", "es-rES", "hi-rIN", "nl-rNL"
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField("String", "TMDB_API_KEY", "\"" + getTMDBApiKey() + "\"")
            buildConfigField("String", "OMDB_API_KEY", "\"" + getOMDBApiKey() + "\"")
        }
        debug {
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField("String", "TMDB_API_KEY", "\"" + getTMDBApiKey() + "\"")
            buildConfigField("String", "OMDB_API_KEY", "\"" + getOMDBApiKey() + "\"")
            minifyEnabled true
        }
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
}

static def getTMDBApiKey() {
    Properties props = new Properties()
    props.load(new FileInputStream(new File('secrets.properties')))
    return props['TMDB_API_KEY']
}

static def getOMDBApiKey() {
    Properties props = new Properties()
    props.load(new FileInputStream(new File('secrets.properties')))
    return props['OMDB_API_KEY']
}


dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'com.google.android.material:material:1.3.0-alpha03'
    implementation 'androidx.palette:palette:1.0.0'
    implementation 'androidx.browser:browser:1.3.0'
    implementation "androidx.work:work-runtime:2.4.0"

    implementation 'com.github.bumptech.glide:glide:4.11.0'
    implementation 'com.mcxiaoke.volley:library:1.0.19'
    implementation 'com.mikhaellopez:circularimageview:3.0.2'
    implementation 'com.miguelcatalan:materialsearchview:1.4.0'
    implementation 'tr.xip.errorview:library:3.0.0'
    implementation 'com.github.paolorotolo:appintro:4.0.0'


    implementation 'com.jakewharton:butterknife:10.1.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.1.0'

    implementation 'com.google.firebase:firebase-appindexing:20.0.0'
    implementation files('libs/YouTubeAndroidPlayerApi.jar')
    debugImplementation 'com.amitshekhar.android:debug-db:1.0.6'
    testImplementation 'junit:junit:4.13.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
